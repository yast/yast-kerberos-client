/**
 * File:	clients/kerberos.ycp
 * Package:	Configuration of kerberos-client
 * Summary:	Main file
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 *
 * Main file for kerberos-client configuration. Uses all other files.
 */

{

/***
 * <h3>Configuration of the kerberos-client</h3>
 */

textdomain "kerberos";

/* The main () */
y2milestone ("----------------------------------------");
y2milestone ("Kerberos-client module started");

import "Kerberos";
import "Wizard";
import "Report";
import "PackageSystem";
import "RichText";
import "CommandLine";

include "kerberos-client/wizards.ycp";

/* is this proposal or not? */
boolean propose = false;
list args = WFM::Args();
if(size(args) > 0) {
    if(is(WFM::Args(0), path) && WFM::Args(0) == .propose) {
        y2milestone("Using PROPOSE mode");
        propose = true;
    }
}

/* main ui function */
any ret = nil;

// --------------------------------------------------------------------------
// --------------------------------- cmd-line handlers

/**
 * Enable or disable Kerberos authentication
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
define boolean KerberosEnableHandler( map<string,string> options ) {
    // check the "command" to be present exactly once
    string command = CommandLine::UniqueOption( options,
	["enable", "disable" ] );
    if( command == nil ) return false;

    Kerberos::use_pam_krb = ( command == "enable" );
    Kerberos::pam_modified = true;
    return true;
}

/**
 * Print summary of basic options
 * @return boolean false
 */
define boolean KerberosSummaryHandler (map options ) {

    CommandLine::Print (RichText::Rich2Plain("<br>"+Kerberos::ShortSummary ()));
    return false; // do not call Write...
}

/**
 * Change basic configuration of Kerberos client (server, realm, domain)
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
define boolean KerberosChangeConfiguration (map options ) {

    boolean ret = false;
    if (options["clockskew"]:"" != "" &&
	options["clockskew"]:"" != Kerberos::clockskew)
    {
	integer value = tointeger (options["clockskew"]:"");
	// FIXME tointeger("blah") returns 0 (bug 115560)
	if (value == nil || value < 0)
	{
	    // error: wrong input (probably string or negative integer)
	    Report::Error (_("The value for clock skew must be a positive integer."));
	    return false;
	}
	Kerberos::clockskew = options ["clockskew"]:"";
	ret = true;
    }
    if (options ["kdc"]:nil != nil && options ["kdc"]:"" != Kerberos::kdc)
    {
	Kerberos::kdc = options ["kdc"]:"";
	ret = true;
    }
    if (options ["domain"]:nil != nil &&
	options ["domain"]:"" != Kerberos::default_domain)
    {
	Kerberos::default_domain = options ["domain"]:"";
	ret = true;
    }
    if (options ["realm"]:nil != nil &&
	options ["realm"]:"" != Kerberos::default_realm)
    {
	Kerberos::default_realm = options ["realm"]:"";
	ret = true;
    }
    if (options["minimum_uid"]:"" != "" &&
	options["minimum_uid"]:"" != Kerberos::minimum_uid)
    {
	Kerberos::minimum_uid	= options ["minimum_uid"]:"";
	ret = true;
    }
    if (ret)
	Kerberos::modified = true;
    return ret;
}

/* the command line description map */
map cmdline = $[
    "id"		: "kerberos",
    // translators: command line help text for Kerberos client module
    "help"		: _("Kerberos client configuration module"),
    "guihandler"	: KerberosSequence,
    "initialize"	: Kerberos::Read,
    "finish"		: Kerberos::Write,
    "actions"		: $[
	"pam" :$[
	    "handler"	: KerberosEnableHandler,
	    // translators: command line help text for pam action
	    "help"	: _("Enable or disable Kerberos authentication")
	],
	"summary" :$[
	    "handler"	: KerberosSummaryHandler,
	    // translators: command line help text for summary action
	    "help"	: _("Configuration summary of Kerberos client")
	],
	"configure"	: $[
	    "handler"	: KerberosChangeConfiguration,
	    // translators: command line help text for configure action
	    "help"	: _("Change the global settings of Kerberos client")
	]
    ],
    "options"		: $[
	"enable"	:$[
	    // translators: command line help text for pam enable option
	    "help"	: _("Enable the service")
	],
	"disable"	:$[
	    // translators: command line help text for pam disable option
	    "help"	: _("Disable the service")
	],
	"kdc"	:$[
	    // translators: command line help text for the kdc option
	    "help"	: _("The Key Distribution Center (KDC) address"),
	    "type"	: "string"
	],
	"domain"	:$[
	    // translators: command line help text for the domain option
	    "help"	: _("Default domain"),
	    "type"	: "string"
	],
	"realm"	:$[
	    // translators: command line help text for the realm option
	    "help"	: _("Default realm"),
	    "type"	: "string"
	],
	"minimum_uid"	:$[
	    // translators: command line help text for the minimum_uid option
	    "help"	: _("Minimum UID used for Kerberos authentication"),
	    "type"	: "string"
	],
	"clockskew"	:$[
	    // translators: command line help text for the clockskew option
	    "help"	: _("Clock skew (in seconds)"),
	    "type"	: "string"
	],
    ],
    "mappings"		: $[
	"pam"		: [ "enable", "disable" ],
	"summary"	: [],
	"configure"	: ["kdc", "domain", "realm", "minimum_uid","clockskew"],
    ]
];

// --------------------------------------------------------------------------

if(propose)
    ret = KerberosAutoSequence();
else
{
    if (!PackageSystem::CheckAndInstallPackagesInteractive (
	Kerberos::UpdatedArchPackages (Kerberos::required_packages)))
	ret = `auto;
    else
	ret = CommandLine::Run (cmdline);
}

y2debug("ret == %1", ret);

/* Finish */
y2milestone("Kerberos-client module finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
